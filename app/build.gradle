apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

    signingConfigs {
        releaseSigningConfig {
            if (System.getenv("TRAVIS")) {
                storeFile rootProject.file('keystore.jks')
                storePassword System.getenv("STORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            } else {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file('../keystore.jks')
                storePassword STORE_PASSWORD
            }


        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "co.gobd.tracker"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 4
        versionName "0.1.4"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSigningConfig
        }
    }

    productFlavors {
        // Development version
        dev {
            manifestPlaceholders = [
                    appName: "GO! Asset Dev"
            ]
        }

        // Production version
        prod {
            manifestPlaceholders = [
                    appName: "GO! Asset"
            ]
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    // Required Google Play Services only, unnecessary packages are skipped
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'

    // Support library
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'

    // Retrofit
    compile('com.squareup.retrofit2:retrofit:2.0.0-beta4') {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'

    // Dagger 2
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Material EditText
    compile 'com.rengwuxian.materialedittext:library:2.1.4'

    // RecyclerView
    compile 'com.android.support:recyclerview-v7:23.3.0'

    // CardView
    compile 'com.android.support:cardview-v7:23.3.0'


}
